struct VertexOutput {
    @builtin(position) position: vec4f,
    @location(0) tex_coord: vec2f,
}

struct EffectUniforms {
    resolution: vec2f,
    _pad_res: vec2f,
    scalars: array<vec4f, 2>,
    vectors: array<vec4f, 2>,
}

@group(0) @binding(0) var input_texture: texture_2d<f32>;
@group(0) @binding(1) var input_sampler: sampler;
@group(1) @binding(0) var<uniform> uniforms: EffectUniforms;

@fragment
fn fragment_main(input: VertexOutput) -> @location(0) vec4f {
    let color = textureSample(input_texture, input_sampler, input.tex_coord);
    let shadows_adj = uniforms.scalars[0].x / 100.0;
    let highlights_adj = uniforms.scalars[0].y / 100.0;

    let luminance = dot(color.rgb, vec3f(0.2126, 0.7152, 0.0722));

    let shadow_weight = 1.0 - smoothstep(0.0, 0.5, luminance);
    let highlight_weight = smoothstep(0.5, 1.0, luminance);

    let shadow_correction = shadow_weight * shadows_adj * 0.5;
    let highlight_correction = highlight_weight * highlights_adj * 0.5;

    let shadow_result = color.rgb + shadow_correction;
    let highlight_result = shadow_result - highlight_correction;

    return vec4f(clamp(highlight_result, vec3f(0.0), vec3f(1.0)), color.a);
}
